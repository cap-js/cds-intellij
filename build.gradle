import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.tasks.VerifyPluginTask


plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.4.0'
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
}

def hasLocalProperties = provider {
    return file('local.properties').exists()
}

if (hasLocalProperties.get()) {
    def props = new Properties()
    file('local.properties').withInputStream { stream ->
        props.load(stream)
    }
    for (prop in props) {
        project.ext[prop.key] = prop.value
    }
}

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
        jetbrainsRuntime()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains:annotations:26.0.2'
    implementation 'org.json:json:20250517'
    implementation 'org.apache.maven:maven-artifact:3.9.11'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.3'
    testCompileOnly 'junit:junit:4.13.2'
    testCompileOnly 'org.junit.jupiter:junit-jupiter:5.13.3'

    intellijPlatform {
        String type = project.findProperty('platformType')
        String version = project.findProperty('platformVersion')
        create(type, version)

        bundledPlugins([
                'com.intellij.java',
                'org.jetbrains.plugins.textmate'
        ])

        plugin("com.redhat.devtools.lsp4ij:0.14.2")

        instrumentationTools()
        pluginVerifier()
        testFramework(TestFrameworkType.Platform.INSTANCE)
        testFramework(TestFrameworkType.Plugin.Java.INSTANCE)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(project.findProperty('javaVersion') as String)
    }
}

sourceSets {
    main {
        java.srcDirs("src/main/java", "src/main/kotlin")
    }
}

intellijPlatform {
    buildSearchableOptions = false
    autoReload = /* set to true to enable plugin reload on FS changes inside plugin */ false
    pluginVerification {
        ides {
            String type = project.findProperty('platformType')
            String version = project.findProperty('platformVersion')
            ide(type, version)
        }
        failureLevel = [
                VerifyPluginTask.FailureLevel.INTERNAL_API_USAGES,
                VerifyPluginTask.FailureLevel.COMPATIBILITY_WARNINGS,
                VerifyPluginTask.FailureLevel.COMPATIBILITY_PROBLEMS,
                VerifyPluginTask.FailureLevel.SCHEDULED_FOR_REMOVAL_API_USAGES,
                VerifyPluginTask.FailureLevel.OVERRIDE_ONLY_API_USAGES,
                VerifyPluginTask.FailureLevel.NON_EXTENDABLE_API_USAGES,
                VerifyPluginTask.FailureLevel.PLUGIN_STRUCTURE_WARNINGS,
                /* VerifyPluginTask.FailureLevel.MISSING_DEPENDENCIES, // due to optional deps */
                VerifyPluginTask.FailureLevel.INVALID_PLUGIN
        ]
    }
}

intellijPlatformTesting.runIde {
    runWebStorm {
        type = IntelliJPlatformType.WebStorm
        if (project.hasProperty('local.ideDir')) {
            localPath = project.file(project.findProperty('local.ideDir'))
        }
        task {
            dependsOn 'copyLspToRunWebStorm', 'syncSandboxIdeConf'
        }
    }
    runIC {
        type = IntelliJPlatformType.IntellijIdeaCommunity
        if (project.hasProperty('local.ideDirIC')) {
            localPath = project.file(project.findProperty('local.ideDirIC'))
        }
        task {
            dependsOn 'copyLspToRunIC', 'syncSandboxIdeConfIC'
        }
    }
}


tasks {
    wrapper {
        gradleVersion = project.findProperty('gradleVersion')
    }

    patchPluginXml {
        version = project.findProperty('pluginVersion')
        sinceBuild = project.findProperty('pluginSinceBuild')
        untilBuild = provider { null }
    }

    buildPlugin {
        dependsOn 'copyLsp'
        archiveFileName = project.findProperty('archiveName')
    }

    compileJava {
        dependsOn 'patchJavaSrc'
    }

    verifyPlugin {
        dependsOn 'copyLsp'
    }

    test {
        dependsOn 'copyLspToInstrumented'
        exclude '**/*TestBase.class'
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    publishPlugin {
        token.set(System.getenv("JB_TOKEN"))
    }

    clean {
        doFirst {
            delete './lsp/node_modules'
        }
    }
}

tasks.register('syncSandboxIdeConf', Copy) {
    onlyIf { hasLocalProperties.get() }
    into "${tasks.named('prepareSandbox_runWebStorm').map { it.defaultDestinationDirectory }.get().get()}/../config"
    into('keymaps') {
        from "${project.findProperty('local.ideConfDir')}/keymaps"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDir')}/options/ide.general.xml"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDir')}/options/laf.xml"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDir')}/options/trusted-paths.xml"
    }
    into('options/linux') {
        from "${project.findProperty('local.ideConfDir')}/options/linux"
    }
    into('options/mac') {
        from "${project.findProperty('local.ideConfDir')}/options/mac"
    }
    into('options/windows') {
        from "${project.findProperty('local.ideConfDir')}/options/windows"
    }
}

tasks.register('syncSandboxIdeConfIC', Copy) {
    onlyIf { hasLocalProperties.get() }
    into "${tasks.named('prepareSandbox_runIC').map { it.defaultDestinationDirectory }.get().get()}/../config"
    into('keymaps') {
        from "${project.findProperty('local.ideConfDirIC')}/keymaps"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDirIC')}/options/ide.general.xml"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDirIC')}/options/laf.xml"
    }
    into('options') {
        from "${project.findProperty('local.ideConfDirIC')}/options/trusted-paths.xml"
    }
    into('options/linux') {
        from "${project.findProperty('local.ideConfDirIC')}/options/linux"
    }
    into('options/mac') {
        from "${project.findProperty('local.ideConfDirIC')}/options/mac"
    }
    into('options/windows') {
        from "${project.findProperty('local.ideConfDirIC')}/options/windows"
    }
}

tasks.register('rmCdsLspNodeModules', Delete) {
    def localRmCdsLspNodeModules = provider {
        return project.findProperty('local.rmCdsLspNodeModules') == 'true'
    }
    onlyIf { localRmCdsLspNodeModules.get() }
    delete 'lsp/node_modules/@sap/cds-lsp'
}

tasks.register('installLsp', Exec) {
    def noNodeModules = provider {
        // re-evaluated on each gradle run
        return !file('lsp/node_modules/@sap/cds-lsp').exists() || project.findProperty('local.rmCdsLspNodeModules') == 'true'
    }
    onlyIf { noNodeModules.get() }
    dependsOn rmCdsLspNodeModules
    workingDir './lsp'
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'npm', 'install', '--no-package-lock'
    } else {
        commandLine 'sh', '-c', 'npm install --no-package-lock'
    }
}

tasks.register('patchJavaSrc', Exec) {
    dependsOn installLsp
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'node', 'src\\js\\codestyle\\patchJavaSrc.js'
    } else {
        commandLine 'sh', '-c', 'src/js/codestyle/patchJavaSrc.js'
    }
}

tasks.register('copyLsp', Copy) {
    mustRunAfter prepareSandbox
    from './lsp'
    into "${tasks.named('prepareSandbox').map { it.pluginDirectory }.get().get()}/lib/cds-lsp"
}

tasks.register('copyLspToRunWebStorm', Copy) {
    mustRunAfter prepareSandbox_runWebStorm
    from './lsp'
    into "${tasks.named('prepareSandbox_runWebStorm').map { it.pluginDirectory }.get().get()}/lib/cds-lsp"
}

tasks.register('copyLspToRunIC', Copy) {
    mustRunAfter prepareSandbox_runIC
    from './lsp'
    into "${tasks.named('prepareSandbox_runIC').map { it.pluginDirectory }.get().get()}/lib/cds-lsp"
}

tasks.register('copyLspToInstrumented', Copy) {
    mustRunAfter prepareTestSandbox
    from './lsp'
    into './build/instrumented/cds-lsp'
}
